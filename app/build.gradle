apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'bugly'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion

    bugly {
        appId = 'a0768ddf34' // 注册时分配的App ID
        appKey = '9d2b9c08-11b7-45d2-b4b4-89753d6df453' // 注册时分配的App Key
        debug = true // 打开debug模式可以观察到日志
        execute = isRelease //插件开关
    }

    defaultConfig {
        applicationId "com.example.hy.wanandroid"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86' // 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
        vectorDrawables.useSupportLibrary = true
        //打包时排除私有项目、android系统库和第三方库中非指定语言的资源文件
        resConfigs "zh", "en"
        multiDexEnabled true
    }

//    signingConfigs {
//        config {
//            storeFile file(KEYPATH)
//            storePassword KEYPASS
//            keyAlias ALIASNAME
//            keyPassword ALIASPASS
//        }
//    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //为项目指定资源目录
    sourceSets {
        main {
            res.srcDirs = ['src/main/res']
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
        //jumboMode true
        //javaMaxHeapSize 4g
        //incremental true
    }

    aaptOptions {
        cruncherEnabled false
    }

    //自定义release apk的输出路径和名称
    android.applicationVariants.all { variant ->
        def curBuildType = variant.buildType.name
        variant.outputs.all { output ->
            //只对release包起作用，如果不是release包则不变更输出路径，否则可能导致AS无法自动安装debug包
            if(curBuildType == "release"){
                //指定输出路径
                output.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/app/release")
                //重新对apk命名
                output.outputFileName = "WanAndroid-release.apk"
            }
        }
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation project(':commonlib')

        //android support
        implementation rootProject.ext.supportDependency
        //custom
        implementation rootProject.ext.customDependency
        //core
        implementation rootProject.ext.coreDependency
        //inject
        implementation rootProject.ext.injectDependency
        //compiler
        annotationProcessor rootProject.ext.compilerDependency
        //ui
        implementation rootProject.ext.uiDependency

        //bugly
        //noinspection GradleDependency
        implementation 'com.tencent.bugly:crashreport:latest.release'
        //noinspection GradleDependency
        implementation 'com.tencent.bugly:nativecrashreport:latest.release'

        //leak test
        debugImplementation rootProject.ext.test["leakCanaryDebug"]
        releaseImplementation rootProject.ext.test["leakCanaryRelease"]
        debugImplementation rootProject.ext.test["leakCanaryDebugSupport"]

        //unit test
        testImplementation rootProject.ext.test["junit"]
        androidTestImplementation rootProject.ext.test["runner"]
        androidTestImplementation rootProject.ext.test["espresso"]
        testImplementation rootProject.ext.test["mocktio"]
    }
}